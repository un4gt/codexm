buildscript {
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }
}

apply plugin: 'com.android.library'
apply plugin: 'org.jetbrains.kotlin.android'

def codexAssetRoot = file('src/main/assets/codex')
def generatedCodexJniLibsDir = file("$buildDir/generated/codexJniLibs")

def generateCodexJniLibsTask = tasks.register('generateCodexJniLibs') {
  // Android 10+ (targetSdk>=29) blocks exec() from app-private writable dirs (/data/data/...).
  // Workaround: package executables as "native libs" so they land in /data/app/.../lib/<abi>/.
  // NOTE: filenames must end with ".so" to be packaged as jniLibs.
  onlyIf { codexAssetRoot.exists() }
  inputs.dir(codexAssetRoot)
  outputs.dir(generatedCodexJniLibsDir)

  doLast {
    generatedCodexJniLibsDir.deleteDir()

    codexAssetRoot.eachDir { abiDir ->
      def abi = abiDir.name
      def outAbiDir = new File(generatedCodexJniLibsDir, abi)
      outAbiDir.mkdirs()

      def mappings = [
        'codex'     : 'libcodex.so',
        'codex-exec': 'libcodex_exec.so',
        'rg'        : 'librg.so',
      ]

      mappings.each { srcName, dstName ->
        def src = new File(abiDir, srcName)
        if (src.exists()) {
          def dst = new File(outAbiDir, dstName)
          dst.bytes = src.bytes
        }
      }
    }
  }
}

android {
  namespace "com.codexm.nativemodules"
  compileSdkVersion safeExtGet('compileSdkVersion', 34)

  defaultConfig {
    minSdkVersion safeExtGet('minSdkVersion', 24)
    targetSdkVersion safeExtGet('targetSdkVersion', 34)

    externalNativeBuild {
      cmake {
        cppFlags "-std=c++17 -fexceptions -frtti"
        arguments "-DANDROID_STL=c++_shared"
      }
    }
  }

  buildFeatures {
    prefab true
  }

  externalNativeBuild {
    cmake {
      path "src/main/cpp/CMakeLists.txt"
    }
  }

  sourceSets {
    main {
      jniLibs.srcDir generatedCodexJniLibsDir
    }
  }
}

tasks.named('preBuild').configure {
  dependsOn(generateCodexJniLibsTask)
}

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib"

  // Prefab (OpenSSL) for libgit2 HTTPS support.
  implementation "com.fpliu.ndk.pkg.prefab.android.21:openssl:3.1.2"
}
